-- Allocator Interface
--
-- This is an interface with additional functions that usually allocators usually should implement.

require 'span'

## function implement_allocator_interface(Allocator)
  local function memcpy(dest: pointer, src: pointer, n: csize): pointer <cimport,cinclude'<string.h>',nodecl> end
  local function memmove(dest: pointer, src: pointer, n: csize): pointer <cimport,cinclude'<string.h>',nodecl> end
  local function memset(s: pointer, c: cint, n: csize): pointer <cimport,cinclude'<string.h>',nodecl> end

  ## Allocator.value.is_allocator = true

  local is_span = #[concept(function(x) return x.type.is_span end)]#
  local Allocator = #[Allocator]#

  ## if not Allocator.value.metafields.alloc0 then
  -- naive implementation for alloc0
  function Allocator:alloc0(size: usize): pointer
    local p: pointer = self:alloc(size)
    if likely(p ~= nilptr and size ~= 0) then
      memset(p, 0, size)
    end
    return p
  end
  ## end

  ## if not Allocator.value.metafields.realloc then
  -- naive implementation for realloc
  function Allocator:realloc(p: pointer, newsize: usize, oldsize: usize): pointer
    if unlikely(p == nilptr) then
      return self:alloc(newsize)
    elseif unlikely(newsize == 0) then
      self:dealloc(p)
      return nilptr
    elseif unlikely(newsize == oldsize) then
      return p
    else -- shrinking or growing
      local newp: pointer = self:alloc(newsize)
      if likely(p ~= nilptr) then
        if likely(newp ~= nilptr and oldsize ~= 0) then
          -- copy the old size when growing or the new size when shrinking
          local minsize: usize = oldsize
          if newsize < oldsize then
            minsize = newsize
          end
          -- use memmove instead of memcpy because we don't really know
          -- if the Allocator can overlap memory
          memmove(newp, p, minsize)
        end
        self:dealloc(p)
      end
      return newp
    end
  end
  ## end

  ## if not Allocator.value.metafields.realloc0 then
  -- naive implementation for realloc0
  function Allocator:realloc0(p: pointer, newsize: usize, oldsize: usize): pointer
    p = self:realloc(p, newsize, oldsize)
    if likely(newsize > oldsize and p ~= nilptr) then
      -- zero the grown part
      memset(&(@*[0]byte)(p)[oldsize], 0, newsize - oldsize)
    end
    return p
  end
  ## end

  ## if not Allocator.value.metafields.spanalloc then
  function Allocator:spanalloc(T: type, size: usize)
    if likely(size > 0) then
      local data: *[0]T = (@*[0]T)(self:alloc(size * #T))
      if likely(data ~= nilptr) then
        return (@span(T)){data,size}
      end
    end
    return (@span(T)){}
  end
  ## end

  ## if not Allocator.value.metafields.spanalloc0 then
  function Allocator:spanalloc0(T: type, size: usize)
    if likely(size > 0) then
      local data: *[0]T = (@*[0]T)(self:alloc0(size * #T))
      if likely(data ~= nilptr) then
        return (@span(T)){data,size}
      end
    end
    return (@span(T)){}
  end
  ## end

  ## if not Allocator.value.metafields.spanrealloc then
  function Allocator:spanrealloc(s: is_span, size: usize)
    local T: type = #[s.type.subtype]#
    local p: *[0]T = (@*[0]T)(self:realloc(s.data, size * #T, s.size))
    if unlikely(size > 0 and p == nilptr) then
      -- reallocation failed, return the original span
      return s
    end
    s.data = p
    s.size = size
    return s
  end
  ## end

  ## if not Allocator.value.metafields.spanrealloc0 then
  function Allocator:spanrealloc0(s: is_span, size: usize)
    local T: type = #[s.type.subtype]#
    local p: *[0]T = (@*[0]T)(self:realloc0(s.data, size * #T, s.size * #T))
    if unlikely(size > 0 and p == nilptr) then
      -- reallocation failed, return the original span
      return s
    end
    s.data = p
    s.size = size
    return s
  end
  ## end

  ## if not Allocator.value.metafields.spandealloc then
  function Allocator:spandealloc(s: is_span): void
    if unlikely(s.size == 0) then return end
    self:dealloc(s.data)
  end
  ## end

  ## if not Allocator.value.metafields.spannew then
  function Allocator:spannew(what: auto, size: usize) <inline>
    ## if what.type.is_type then
    local T: type = what
    local spn: span(T) = self:spanalloc0(T, size)
    assert(spn.size == size, 'out of memory')
    ## else
    local T = #[what.type]#
    local spn: span(T) = self:spanalloc(T, size)
    assert(spn.size == size, 'out of memory')
    for i:usize=0,<size do
      memcpy(&spn[i], &what, #T)
    end
    ## end
    return spn
  end
  ## end

  ## if not Allocator.value.metafields.new then
  function Allocator:new(what: auto) <inline>
    ## if what.type.is_type then
    local T: type = what
    local ptr: *T = (@*T)(self:alloc0(#T))
    assert(ptr ~= nilptr, 'out of memory')
    ## else
    local T: type = #[what.type]#
    local ptr: *T = (@*T)(self:alloc(#T))
    assert(ptr ~= nilptr, 'out of memory')
    memcpy(ptr, &what, #T)
    ## end
    return ptr
  end
  ## end

  ## if not Allocator.value.metafields.delete then
  function Allocator:delete(s: #[concept(function(x)
    if x.type.is_pointer or x.type.is_span then
      return true
    end
    return false, string.format("type '%s' is not a span or a pointer", x.type)
  end)]#): void <inline>
    ## if s.type.is_span then
      self:spandealloc(s)
    ## else -- pointer
      self:dealloc(s)
    ## end
  end
  ## end

  -- Checked allocation wrappers.

  function Allocator:xalloc(size: usize): pointer <inline>
    local p: pointer = self:alloc(size)
    assert(p ~= nilptr, 'out of memory')
    return p
  end

  function Allocator:xalloc0(size: usize): pointer <inline>
    local p: pointer = self:alloc0(size)
    assert(p ~= nilptr, 'out of memory')
    return p
  end

  function Allocator:xrealloc(p: pointer, newsize: usize, oldsize: usize): pointer <inline>
    local p: pointer = self:realloc(p, newsize, oldsize)
    assert(p ~= nilptr, 'out of memory')
    return p
  end

  function Allocator:xrealloc0(p: pointer, newsize: usize, oldsize: usize): pointer <inline>
    local p: pointer = self:realloc0(p, newsize, oldsize)
    assert(p ~= nilptr, 'out of memory')
    return p
  end

  function Allocator:spanxalloc(T: type, size: usize) <inline>
    local s: span(T) = self:spanalloc(T, size)
    assert(s.size == size, 'out of memory')
    return s
  end

  function Allocator:spanxalloc0(T: type, size: usize) <inline>
    local s: span(T) = self:spanalloc0(T, size)
    assert(s.size == size, 'out of memory')
    return s
  end

  function Allocator:spanxrealloc(s: is_span, size: usize) <inline>
    s = self:spanrealloc(s, size)
    assert(s.size == size, 'out of memory')
    return s
  end

  function Allocator:spanxrealloc0(s: is_span, size: usize) <inline>
    s = self:spanrealloc0(s, size)
    assert(s.size == size, 'out of memory')
    return s
  end
## end
